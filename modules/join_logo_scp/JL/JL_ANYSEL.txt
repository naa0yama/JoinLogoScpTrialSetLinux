# join_logo_scp用の実行内容スクリプト
#
# 内容：放送局とタイトル情報からJLスクリプトを選択して読み込む動作
#
#       Amatsukazeによる起動想定で、対応する環境変数が定義されている前提。
#       （対応バージョンを入れるなどで対応）
#       環境変数設定（CLI_IN_PATHとSERVICE_IDまたはSERVICE_ABBR）があれば
#       Amatsukaze関係なく使用可能。
#
# 次の順番でファイルが存在したら選択される
# （[環境変数TS]の所は、TS_IN_PATH、定義なければCLI_IN_PATHが使用される）
# 1. {環境変数TS}.trim.avs
#      trim情報を直接設定。div.txtもあれば使用
# 2. {環境変数TSから拡張子.tsを除く}.keyframe
#      trim情報またはカット位置を直接設定。div.txtもあれば使用
# 3. JL_ini_選択読込.txt
#      放送局とタイトルの文字列一致から選択
#

#----------------------------------------------
# 設定
#----------------------------------------------
Call "JL_ini_外部環境取得.txt"               # 外部設定
Default file_ts      ""                      # 未定義時の初期設定
Default file_div     "${file_ts}.div.txt"    # 分割位置記載の入力ファイル名
Default file_trim    "${file_ts}.trim.avs"   # Trim位置記載の入力ファイル名
Default file_calltr  "JL_位置読込.txt"       # trim情報を直接設定するスクリプト
Default file_inisel  "JL_ini_選択読込.txt"   # JLスクリプトの選択読込スクリプト
Default file_keyf    "${file_ts:r}.keyframe" # keyframe入力ファイル名
Default keyf_fromcut 1                       # keyframe最初データ位置から（0=残す 1=カット）

#----------------------------------------------
# 動作
#----------------------------------------------
#--- Trim直接指定あれば使用する ---
Set use_trim_direct 0
Set use_trim_keepcut 0
ReadCheck "${file_trim}" -RegOut found_trim -local
ReadCheck "${file_keyf}" -RegOut found_keyf -local
If ${found_trim} || ${found_keyf}
  If ${found_trim} {      # avs-Trim
    Set type_trim_in 2
    Set use_trim_keepcut 1  # カット位置は直接Amatsukazeが読み込むので補正なし
  }ElsIf ${found_keyf} {  # keyframe
    Set type_trim_in 3
  };
  ReadCheck "${file_div}" -RegOut use_divfile  # 分割ファイル使用選択
  Set use_trim_direct 1    # 直接Trim位置を指定する
  # 実行はChParamJL*.csvのOPTIONSで変数設定してから呼び出せるように後で行う
EndIf

#--- JLスクリプト選択処理 ---
Call "${file_inisel}"

#--- 上のCallで設定される情報 ---
Default jlsi_optstr  ""                # 追加オプション文字列
Default jlsi_jlrun   ""                # CALLスクリプト
Default jlsi_comment ""                # 選択コメント

#--- 直接trim位置設定時のJL_RUNを位置読込に変更 ---
If ${use_trim_direct}>0 {
  LocalSet cmt "直接位置指定"
  If ${use_divfile} { Set cmt "${cmt} （分割指定ファイルあり）" };
  Set jlsi_jlrun   "${file_calltr}"
  Set jlsi_comment "${cmt}"
};
#--- Amatsukazeが直接入力ファイルの位置を読む時は内側にカット補正しない ---
If ${use_trim_keepcut}
  If outrev_cutframe { Set outrev_cutframe 0 };
EndIf

Echo 検索対象 : ${file_title}
Echo 放送局   : ${service_abbr} : ${service_id}
Echo 選択コメント : ${jlsi_comment}
Echo 選択スクリプト : ${jlsi_jlrun}

#--- 追加オプション設定 ---
If "${jlsi_optstr:Xq}"!="" {   # 変数内ダブルクォートは通常文字として認識させる
  Echo 追加オプション : ${jlsi_optstr}
  OptSet ${jlsi_optstr}
};

#--- Call動作 ---
If "${jlsi_jlrun}"!="" {
  Call "${jlsi_jlrun}"
};
